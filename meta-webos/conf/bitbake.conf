# Copyright (c) 2014-2024 LG Electronics, Inc.
#
# meta-webos/conf/bitbake.conf
#
# Add settings here to override the defaults in oe-core/meta/conf/bitbake.conf
#
# Using ?= instead of = to allow overriding from webos-local.conf .
#


# This assignment can't be in webos_distro_variant_dep.bbclass because
# WEBOS_TARGET_DISTRO_VARIANT used to form filenames for global require-s that
# are always done.
WEBOS_TARGET_DISTRO_VARIANT ?= "normal"

# Nothing is appended to DISTRO_NAME for the default distro variant when
# forming the response to the NYX_OS_INFO_WEBOS_NAME query.
WEBOS_DISTRO_NAME_SUFFIX ?= ""

# The overrides for WEBOS_TARGET_DISTRO_VARIANT must be the override choices ahead
# of DISTRO, so place them immediately to the right of it in DISTROOVERRIDES. Two
# overrides are added: "distrovariant-<WEBOS_TARGET_DISTRO_VARIANT>", which is
# meant to be used when an override applies to all DISTRO-s that have a particular
# variant, and
# "<DISTRO>-<WEBOS_TARGET_DISTRO_VARIANT>", which is meant to be used when an
# override applies to a particular DISTRO with that variant.
# Only add the overrides if the recipe has inherited webos_distro_variant_dep.
WEBOS_DISTROOVERRIDES_DISTRO_VARIANT ?= "${@ \
    ':${DISTRO}-${WEBOS_TARGET_DISTRO_VARIANT}:distrovariant-${WEBOS_TARGET_DISTRO_VARIANT}' \
    if bb.data.inherits_class('webos_distro_variant_dep', d) else \
    '' \
}"
WEBOS_DISTROOVERRIDES_DISTRO_VARIANT[vardepvalue] = "${WEBOS_DISTROOVERRIDES_DISTRO_VARIANT}"

DISTROOVERRIDES ?= "${@d.getVar('DISTRO') or ''}${WEBOS_DISTROOVERRIDES_DISTRO_VARIANT}"

# Add SOC_FAMILY to the machine overrides when a recipe has inherited from
# webos_soc_family_dep and SOC_FAMILY has been set (in MACHINE.conf). This is a
# different implementation from that in oe-core/meta/conf/machine/include/soc-family.inc
# so that we can require that webos_soc_family_dep be inherited by any recipe
# that uses SOC_FAMILY.
WEBOS_MACHINEOVERRIDES_SOC_FAMILY ?= "${@ \
    d.getVar('SOC_FAMILY') + ':' \
    if bb.data.inherits_class('webos_soc_family_dep', d) and (d.getVar('SOC_FAMILY') or '') != '' else \
    '' \
}"

MACHINEOVERRIDES ?= "${WEBOS_MACHINEOVERRIDES_SOC_FAMILY}${MACHINE}"

require ${COREBASE}/meta/conf/bitbake.conf
