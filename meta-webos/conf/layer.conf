# Copyright (c) 2012-2024 LG Electronics, Inc.

BBPATH ?= ""
# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"
# We have recipe directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb ${LAYERDIR}/recipes-*/*/*.bbappend"

# Exclude unused recipies from meta-security, use only smack
BBMASK:append = " meta-security\/(?!.*\/smack\/).*"
# Disable the check added in: https://git.yoctoproject.org/cgit/cgit.cgi/meta-security/commit/?id=c1235f6affb5c38e64b3a04533b8388969b194b2
SKIP_META_SECURITY_SANITY_CHECK = "1"

BBFILE_COLLECTIONS += "meta-webos"
BBFILE_PATTERN_meta-webos := "^${LAYERDIR}/"
BBFILE_PRIORITY_meta-webos = "40"

LAYERSERIES_COMPAT_meta-webos = "kirkstone"

LAYERDEPENDS_meta-webos = " \
    core \
    openembedded-layer \
    qt6-layer \
    filesystems-layer \
    networking-layer \
    multimedia-layer \
    meta-python \
    meta-webos-backports-4.1 \
    meta-webos-backports-4.2 \
    meta-webos-backports-4.3 \
    meta-webos-backports-5.0 \
    security \
"

# Remove openembedded-layer perl-layer networking-layer meta-python
# dependencies from meta-security layer
# OVERRIDE for LAYERDEPENDS_security set in the meta-security layer
LAYERDEPENDS_security = "core"

BBFILES_DYNAMIC += " \
    clang-layer:${LAYERDIR}/dynamic-layers/clang-layer/*/*/*.bb \
    clang-layer:${LAYERDIR}/dynamic-layers/clang-layer/*/*/*.bbappend \
"

# Set these variables to build webos outside of build-webos (for OSE only).
# https://github.com/webosose/meta-webosose/issues/28
META_WEBOS_LAYER ??= "${LAYERDIR}"
META_OE_LAYER ??= "${@'${BBFILE_PATTERN_openembedded-layer}'[1:-1]}"

# Prepend scripts to WEBOS_EXTRA_PATH
WEBOS_EXTRA_PATH .= "${@'${BBFILE_PATTERN_meta-webos}'[1:] + 'scripts:'}"

# Set path to qemu image tests included in this layer
QEMUIMAGETESTS := "${COREBASE}/scripts/qemuimage-tests"

# Add extra license path
LICENSE_PATH += "${LAYERDIR}/licenses"

SIGGEN_EXCLUDERECIPES_ABISAFE += " \
    webos-initscripts \
    luna-prefs-data \
    upstart-getty \
    settingsservice-conf \
"

# TODO: introduced long time ago, double check if still needed
# http://gpro.lge.com/c/webos-pro/meta-lg-webos/+/128601/ glibc-mtrace, glibc-scripts, resolvconf: Exclude dependency on perl and busybox
SIGGEN_EXCLUDE_SAFE_RECIPE_DEPS += " \
    glibc-mtrace->perl \
    glibc-scripts->busybox \
    resolvconf->busybox \
"

# This is a bit weird
# gcc-cross-${TARGET_ARCH}->linux-libc-headers
# was already added in 2014 with:
# https://git.openembedded.org/openembedded-core/commit/?id=97df4e8e61a
# but with multilib this doesn't work and it's not about missing ${MLPREFIX} or lib32- prefix
# it's the ${TARGET_ARCH} which is possibly expanded too late even when in bitbake -e it looks right
SIGGEN_EXCLUDE_SAFE_RECIPE_DEPS += " \
    gcc-cross-arm->linux-libc-headers \
"

# lsb-release is rebuilt every time due to BUILD_ID changing
# in every jenkins job due to BUILD_NUMBER or in local builds
# whenever DATETIME changes
SIGGEN_EXCLUDE_SAFE_RECIPE_DEPS += " \
    bootchart2->lsb-release \
    keyutils->lsb-release \
    nyx-modules->lsb-release \
    packagegroup-core-boot->lsb-release \
"
